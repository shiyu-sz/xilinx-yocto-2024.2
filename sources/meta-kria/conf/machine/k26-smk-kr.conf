#@TYPE: Machine
#@NAME: Generic Kria KR260 Robotics Starter Kit
#@DESCRIPTION: Machine configuration for Kria KR260 Robotics Starter Kit (Kria K26 SOM + KR260 carrier board).

#### Preamble
MACHINEOVERRIDES =. "${@['', 'k26-smk-kr:']['k26-smk-kr' != '${MACHINE}']}"
MACHINEOVERRIDES =. "k26-kr:"
#### Regular settings follow

require conf/machine/include/k26-smk.inc
require conf/machine/include/kria-qemu-kr.inc

INITRAMFS_IMAGE ?= "petalinux-initramfs-image"

# This is an 'XSCT' based BSP
XILINX_XSCT_VERSION = "2024.2"
XILINX_WITH_ESW = "xsct"

# Add system XSA
HDF_URI = 'https://petalinux.xilinx.com/sswreleases/rel-v2024.2/hdf-examples/2024.2/2024.2_1111_1_02260501/external/k26-smk-kr/k26-smk-kr-2024.2_1111_1.xsa'
HDF_URI[sha256sum] = '479444deab222bebd452d9555277b5456a027a3444543ca0e9b529c8e8168831'

UBOOT_DT_FILES = "\
    zynqmp-sck-kr-g-revA.dtso \
    zynqmp-sck-kr-g-revB.dtso \
    "

# DTB_FILE_NAME cant be changed globally or it will break the build but we can modify it for these
# tasks specifically so that the resulting symlink points to the correct file for a specific board
DTB_FILE_NAME:task-install:pn-device-tree = "${UBOOT_DTFILE_PREFIX}-zynqmp-sck-kr-g-revB.dtb"
DTB_FILE_NAME:task-deploy:pn-device-tree = "${UBOOT_DTFILE_PREFIX}-zynqmp-sck-kr-g-revB.dtb"
# Workaround for bitbake not detecting the above change in the hash
PR:append:pn-device-tree = ".1"

#### No additional settings should be after the Postamble
#### Postamble
PACKAGE_EXTRA_ARCHS:append = "${@['', ' k26_smk_kr']['k26-smk-kr' != "${MACHINE}"]}"
