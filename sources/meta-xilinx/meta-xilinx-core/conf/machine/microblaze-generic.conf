#@TYPE: Machine
#@NAME: microblaze-generic
#@DESCRIPTION: Machine configuration for the microblaze-generic devices

#### Preamble
MACHINEOVERRIDES =. "${@['', 'microblaze-generic:']['microblaze-generic' != '${MACHINE}']}"
#### Regular settings follow

# Set the default for a modern full feature microblaze...
TUNE_FEATURES:tune-microblaze ?= "microblaze v11.0 pattern-compare barrel-shift divide-hard multiply-high fpu-hard reorder"
DEFAULTTUNE ?= "microblaze"

# Variables that changes based on hw design or board specific requirement must be
# defined before calling the required inclusion file else pre-expansion value
# defined in local.conf without machine override will not be reflected.

# Yocto Microblaze u-boot-xlnx variables
UBOOT_MACHINE ?= "microblaze-generic_defconfig"
UBOOT_INITIAL_ENV = ""
BOOTMODE ?= "generic.root"

# Yocto Microblaze KERNEL Variables
UBOOT_ENTRYPOINT  ?= "0x80000000"
UBOOT_LOADADDRESS ?= "0x80000000"
KERNEL_EXTRA_ARGS += "UIMAGE_LOADADDR=${UBOOT_ENTRYPOINT}"

# Microblaze Serial Console settings
SERIAL_CONSOLES ?= "115200;ttyUL0"

require conf/machine/include/soc-tune-include.inc
require conf/machine/include/machine-xilinx-default.inc
require conf/machine/include/machine-xilinx-qemu.inc

# The default MACHINE_ARCH is dynmic for microblaze, since the architecture is not fixed
# Based on core bitbake.conf
DEF_MACHINE_ARCH = "${@[d.getVar('TUNE_PKGARCH'), d.getVar('MACHINE')][bool(d.getVar('MACHINE'))].replace('-', '_')}"
# Enable a dynamic machine_arch
MB_MACHINE_ARCH = "${@[d.getVar('TUNE_PKGARCH'), d.getVar('TUNE_PKGARCH') + '-generic'][bool(d.getVar('MACHINE'))].replace('-', '_')}"

MACHINE_ARCH = "${@['${MB_MACHINE_ARCH}', '${DEF_MACHINE_ARCH}']['microblaze-generic' != "${MACHINE}"]}"

MACHINE_FEATURES = ""

KERNEL_IMAGETYPE ?= "linux.bin.ub"
KERNEL_IMAGETYPES = ""

EXTRA_IMAGEDEPENDS += " \
    virtual/bootloader \
    virtual/elfrealloc \
    u-boot-xlnx-scr \
    "

IMAGE_FSTYPES += "cpio.gz"

# Microblaze QEMU Configurations
QB_MEM ?= "-m 2G"
QB_KERNEL_CMDLINE_APPEND ?= "console=ttyUL0,115200 root=/dev/ram0 rw"
QB_NETWORK_DEVICE ?= "-net nic,netdev=net0,macaddr=@MAC@"

# This will work with the default runqemu, as the first serial port is the
# correct console
#
# One total serial port defined in this model (according to the generated dts)
#
# hw serial0 axi_uartlite_0 (40600000) - linux serial0 (ttyUL0)
QB_XILINX_SERIAL ?= ""

#### No additional settings should be after the Postamble
#### Postamble
PACKAGE_EXTRA_ARCHS:append = "${@['', ' ${MB_MACHINE_ARCH}']['microblaze-generic' != "${MACHINE}"]}"
